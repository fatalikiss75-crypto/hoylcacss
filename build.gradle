import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'ru.nbk'
version = '1.21.11'
description = 'HolyCases - Плагин на кейсы с донатом для Minecraft 1.21+'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://maven.enginehub.org/v1snapshots' }
}

dependencies {
    // Spigot API
    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'

    // PlaceholderAPI (опционально)
    compileOnly 'me.clip:placeholderapi:2.11.6'

    // HikariCP для работы с MySQL
    implementation 'com.zaxxer:HikariCP:5.1.0'

    // JDBI для работы с базой данных
    implementation 'org.jdbi:jdbi3-core:3.45.1'
    implementation 'org.jdbi:jdbi3-caffeine-cache:3.45.1'
    implementation 'org.jdbi:jdbi3-sqlobject:3.45.1'

    // Caffeine для кэширования
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    // JSON
    implementation 'org.json:json:20240303'

    // Apache HttpClient
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'

    // Unirest
    implementation 'com.konghq:unirest-java:3.14.1'

    // Commons Lang3
    implementation 'org.apache.commons:commons-lang3:3.17.0'
}

tasks {
    compileJava {
        options.encoding = 'UTF-8'
        options.release = 17
    }

    shadowJar {
        archiveClassifier = ''
        archiveFileName = "${project.name}-${project.version}.jar"

        minimize()

        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
        exclude 'module-info.class'

        relocate 'com.zaxxer', 'ru.nbk.rolecases.libs.hikari'
        relocate 'org.jdbi', 'ru.nbk.rolecases.libs.jdbi'
        relocate 'com.github.benmanes', 'ru.nbk.rolecases.libs.caffeine'
        relocate 'org.json', 'ru.nbk.rolecases.libs.json'
        relocate 'org.apache', 'ru.nbk.rolecases.libs.apache'
    }

    build {
        dependsOn shadowJar
    }
}
